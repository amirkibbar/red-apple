buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

group = "ajk.gradle.consul"
version = "0.1.6"

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

compileGroovy {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "de.undercouch:gradle-download-task:2.1.0"
    compile "org.apache.httpcomponents:fluent-hc:4.5.2"
    compile "com.jcraft:jsch:0.1.53"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'a gradle plugin for starting and stopping Consul during integration tests')
                root.children().last() + pomConfig
            }
        }
    }
}

task install(dependsOn: 'publishMavenPublicationToMavenLocal') << {
    logger.info "installing $project.name"
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/gradle-consul-plugin-${project.version}.pom")
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publications = ['maven']

    publish = true

    pkg {
        repo = 'maven'
        name = 'gradle-consul-plugin'
        websiteUrl = 'https://github.com/amirkibbar/red-apple'
        issueTrackerUrl = 'https://github.com/amirkibbar/red-apple/issues'
        vcsUrl = 'https://github.com/amirkibbar/red-apple.git'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'consul']
        version {
            name = project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/amirkibbar/red-apple'
    vcsUrl = 'https://github.com/amirkibbar/red-apple.git'
    description = 'a Consul plugin for gradle to help running Consul during integration test and to allow registering and removing services and checks from a running Consul'
    tags = ['consul']

    plugins {
        greetingsPlugin {
            id = 'ajk.gradle.consul'
            displayName = 'Gradle Consul plugin'
        }
    }

    mavenCoordinates {
        groupId = "ajk.gradle.consul"
        artifactId = "gradle-consul-plugin"
        version = project.version
    }
}

bintrayUpload.dependsOn = [sourcesJar, javadocJar]
publishPlugins.dependsOn = [bintrayUpload]

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
